(ql:quickload :cl-base64)
(ql:quickload :cl-ppcre)

(defun to-bytes (str)
  (map 'list #'char-code str))

(defvar *bytes*
  (with-open-file (instream "6.txt")
    (to-bytes
      (cl-base64:base64-stream-to-string instream))))

(defun different-right-bit? (byte1 byte2)
  (/= (logand byte1 1) (logand byte2 1)))

(defun byte-distance (byte1 byte2)
  (labels ((_ (b1 b2 i acc)
              (if (zerop i)
                acc
                (_ (ash b1 -1)
                   (ash b2 -1)
                   (1- i)
                   (if (different-right-bit? b1 b2)
                     (1+ acc)
                     acc)))))
    (_ byte1 byte2 8 0)))

(defun hamming (bytes1 bytes2)
  (labels ((_ (bytes1 bytes2 acc)
              (if (null bytes1)
                acc
                (_ (cdr bytes1)
                   (cdr bytes2)
                   (+ acc (byte-distance (car bytes1)
                                         (car bytes2)))))))
    (_ bytes1 bytes2 0)))

(defun in-slices (fn n lst)
  (labels ((_ (i lacc lst prev)
              (cond ((null lst) nil)
                    ((= i n)
                     (let ((cur (nreverse lacc)))
                       (funcall fn prev cur)
                       (_ 0 nil lst cur)))
                    (t
                      (_ (1+ i)
                         (cons (car lst) lacc)
                         (cdr lst)
                         prev)))))
    (_ 0 nil lst nil)))

(defun map-slice (n lst)
  (let (acc)
    (in-slices #'(lambda (prev cur)
                   (declare (ignore prev))
                   (push cur acc))
               n
               lst)
    (nreverse acc)))

(defun most-likely-key-size (bytes)
  (let (gacc)
    (do ((key-size 2 (1+ key-size)))
        ((= key-size 40))
      (let ((acc 0)
            (cnt 0))
        (in-slices #'(lambda (bytes1 bytes2)
                       (when (and bytes1 bytes2)
                         (incf acc (/ (hamming bytes1 bytes2) key-size))
                         (incf cnt)))
                   key-size
                   bytes)
        (push (cons (/ acc cnt) key-size) gacc)))
    (cdar (sort gacc #'< :key #'car))))

(defun repeat-key-xor (bytes key)
  (format nil "窿磲疸狎＇灬礅溽ㄢ翦ㄣ镤瀛汨狎祜瑛矧怡翦脲┅怡翦螬┅ㄤ彐躅筱矧篝颟戾铉翳ㄣ飙痧泸搴犰飙磲翥桢蟓狍篝蜷铉块┷弭犸轭箬蜢漉茛篝颟┅换泸徙翳脲ㄦ矧磲窿ア磲疸狎＇灬礅溽ㄢ翦螬戾è磲癌翳瀛脲铋飑ㄤè脲珲弩ū脲珲弩螬┅è脲珲弩辈珐ㄣ镤瀛汨狎翳瀛脲┅戾è蝈筱矧蝈疱狒脲矧怡翦脲珲弩螬┅麒孱蝈磲箦翩磲蝈翳瀛脲脲珲弩螬┅┅ㄡ痧禊＇磲疸狎＇扉篝磲瓠箪殂盹篝扉脲禊脲箝怡翦螵怡翦螵┅┅换⒃弪黹钺麸睾买轭翳铒轶澧